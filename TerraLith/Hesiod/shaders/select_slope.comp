#version 450

layout(local_size_x = 16, local_size_y = 16) in;

layout(set = 0, binding = 0) buffer InputBuf  { float data[]; } input_buf;
layout(set = 0, binding = 1) buffer OutputBuf { float data[]; } output_buf;

layout(push_constant) uniform PushConstants
{
  uint width;
  uint height;
} pc;

float get_height(int x, int y)
{
  int cx = clamp(x, 0, int(pc.width) - 1);
  int cy = clamp(y, 0, int(pc.height) - 1);
  return input_buf.data[cy * pc.width + cx];
}

void main()
{
  uvec2 gid = gl_GlobalInvocationID.xy;
  if (gid.x >= pc.width || gid.y >= pc.height)
    return;

  int ix = int(gid.x);
  int iy = int(gid.y);

  // Compute gradient using central differences with edge clamping
  float dx = 0.5 * (get_height(ix + 1, iy) - get_height(ix - 1, iy));
  float dy = 0.5 * (get_height(ix, iy + 1) - get_height(ix, iy - 1));

  // Gradient magnitude (slope)
  uint idx = gid.y * pc.width + gid.x;
  output_buf.data[idx] = sqrt(dx * dx + dy * dy);
}
